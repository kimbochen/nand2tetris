// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM4K.hdl

/**
 * Memory of 4K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    DMux8Way(
        in=load, sel=address[9..11],
        a=loadram0, b=loadram1, c=loadram2, d=loadram3,
        e=loadram4, f=loadram5, g=loadram6, h=loadram7
    );

    RAM512(in=in, load=loadram0, address=address[0..8], out=dout0);
    RAM512(in=in, load=loadram1, address=address[0..8], out=dout1);
    RAM512(in=in, load=loadram2, address=address[0..8], out=dout2);
    RAM512(in=in, load=loadram3, address=address[0..8], out=dout3);
    RAM512(in=in, load=loadram4, address=address[0..8], out=dout4);
    RAM512(in=in, load=loadram5, address=address[0..8], out=dout5);
    RAM512(in=in, load=loadram6, address=address[0..8], out=dout6);
    RAM512(in=in, load=loadram7, address=address[0..8], out=dout7);

    Mux8Way16(
        a=dout0, b=dout1, c=dout2, d=dout3,
        e=dout4, f=dout5, g=dout6, h=dout7,
        sel=address[9..11], out=out
    );
}
