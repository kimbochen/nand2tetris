// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

/*
 * Four actions are encoded as:
 * Hold  00
 * Reset 01
 * Inc   10
 * Load  11
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Or(a=reset, b=load, out=act0);
    Not(in=reset, out=nr); Or(a=load, b=inc, out=lORi); And(a=nr, b=lORi, out=act1);

    Inc16(in=dout, out=incout);

    Mux4Way16(
        a=dout, b=false, c=incout, d=in,
        sel[0]=act0, sel[1]=act1, out=data
    );

    DFF16(in=data, out=dout, out=out);
}
